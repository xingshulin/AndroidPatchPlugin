buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'groovy'

repositories {
    jcenter()
}
dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'commons-codec:commons-codec:1.6'
}

def currentVersion = '0.4.0'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourceJar
            groupId 'com.xingshulin.singularity'
            artifactId 'patch-generator-plugin'
            version currentVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A gradle plugin for generation android app hotfix')
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'maven'
        name = 'android-patch-generator'
        userOrg = 'xingshulin'
        licenses = ['Apache-2.0']
        publish = true
        publicDownloadNumbers = true
        vcsUrl = 'https://github.com/xingshulin/AndroidPatchPlugin.git'
        version {
            desc = 'android patch generator v' + currentVersion
            name = currentVersion
            released = new Date()
        }
    }
}